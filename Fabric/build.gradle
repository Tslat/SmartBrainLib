plugins {
    id 'ml-platform-config'
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'net.darkhax.curseforgegradle' version '1.1.24'
    id 'com.modrinth.minotaur' version '2.8.7'
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${mappings_mc_version}:${mappings_version}@zip")
    }

    modCompileOnly "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modLocalRuntime "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modCompileOnly "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
    modLocalRuntime "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
}

loom {
    def accessWidenerFile = project(":common").file("src/main/resources/${mod_id}.accesswidener")

    if (accessWidenerFile.exists())
        accessWidenerPath.set(accessWidenerFile)

    mixin {
        defaultRefmapName.convention("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDirectory.asFile.get() as File
    }
}

task publishToCurseForge(type:  net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    group = "publishing"
    apiToken = System.getenv("CURSEFORGE_TOKEN") ?: "Invalid/No API Token Found"

    def mainFile = upload(661293, tasks.remapJar)
    mainFile.releaseType = "release"
    mainFile.addModLoader("Fabric")
    mainFile.addGameVersion(minecraft_version)
    mainFile.addJavaVersion("Java 21")
    mainFile.changelog = "${changelog_url}"
    mainFile.displayName = "${project.mod_name} Fabric ${project.minecraft_version}-${project.version}"

    //https://github.com/Darkhax/CurseForgeGradle#available-properties
}

modrinth {
    token = System.getenv("modrinthKey")
    projectId = mod_id
    versionNumber = project.version
    versionType = "release"
    uploadFile = remapJar
    gameVersions = ["${project.minecraft_version}"]
    loaders = ["fabric"]
}