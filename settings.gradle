pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()

        // Fabric
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Fabric'
                    url = uri("https://maven.fabricmc.net")
                }
            }
            filter {
                includeGroup("net.fabricmc")
                includeGroup("fabric-loom")
            }
        }

        // NeoForge
        exclusiveContent {
            forRepository {
                maven {
                    name = 'NeoForge'
                    url = uri("https://maven.neoforged.net/releases")
                }
            }
            filter {
                includeGroupAndSubgroups("net.neoforged")
                includeGroup("codechicken")
            }
        }

        // Forge
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Forge'
                    url = uri("https://maven.minecraftforge.net/")
                }
            }
            filter {
                includeGroupAndSubgroups("net.minecraftforge")
            }
        }

        // Mixin
        exclusiveContent {
            forRepository {
                maven {
                    name = 'SpongeForge'
                    url = uri("https://repo.spongepowered.org/repository/maven-public")
                }
            }
            filter {
                includeGroupAndSubgroups("org.spongepowered")
            }
        }

        // Parchment
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Parchment'
                    url = uri("https://maven.parchmentmc.org")
                }
            }
            filter {
                includeGroupAndSubgroups("org.parchmentmc")
            }
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

rootProject.name = 'SmartBrainLib'
include("common")
include("fabric")
include("neoforge")