subprojects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'

	java.toolchain.languageVersion = JavaLanguageVersion.of(17)
	java.withSourcesJar()

	repositories {
		mavenCentral()

		maven {
			name = 'Sponge / Mixin'
			url = 'https://repo.spongepowered.org/repository/maven-public/'
		}

		maven {
			name = "Forge"
			url = 'https://maven.minecraftforge.net'
		}
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = 'UTF-8'
		it.options.release = 17
	}

	tasks.withType(GenerateModuleMetadata) {
		enabled = false
	}

	publishing {
		repositories {
			maven {
				name = "Repsy"
				url 'https://repo.repsy.io/mvn/tslat/tslatentitystatus'
				credentials {
					username project.findProperty("repsy.user") ?: System.getenv("REPSY_USERNAME")
					password project.findProperty("repsy.password") ?: System.getenv("REPSY_PASSWORD")
				}
			}
		}
	}
}
